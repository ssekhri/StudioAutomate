--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _76vFIKjdEeeagptQvCTsXg,_77JU0KjdEeeagptQvCTsXg,_770DMKjdEeeagptQvCTsXg
Element-Name: createAlloyWithAppleWatchExtension
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _835QULVnEee4B903svdkWw
Runtime-Version: 2.2.0.201706152316
Save-Time: 1/24/18, 7:47 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

//Global variable for Watch OS Project Name
global [val watchOSProjName "SampleWatchOSProj"]

// Create a new Alloy default project=
get-menu "File/New/Mobile App Project" | click
with [get-window "New Mobile App Project"] {
   get-table | select Alloy
    get-label -after [get-label "Available Templates:"] | click
    get-button "Next >" | click
    
    get-editbox -after [get-label "Project name:"] | set-text TestAlloyApp
    with [get-group "Project Settings"] {
        get-editbox -after [get-label "App Id:"] | set-text "alloy.appc.smoketests"
        // Get the Ti SDK value from Global Parameter context
        get-combo -after [get-label "Titanium SDK Version:"] | select [get-param "$TI_SDK"]
    }
}
with [get-window "New Mobile App Project" | get-group "Axway Appcelerator Platform Services" 
    | get-button "Enable Axway Appcelerator Platform Services"] {
    	
	if [get-property selected -raw | equals true]
	{
	 	get-window "New Mobile App Project" | get-group "Axway Appcelerator Platform Services" | get-button "Enable Axway Appcelerator Platform Services" | uncheck
	}
}

with [get-window "New Mobile App Project"] {
	get-button Finish | click
}

//Wait until window for New Project creation is closed
loop {
	if [get-window | get-property "getShell().getText()" -raw | eq "New Mobile App Project"] {
		wait 1000
		recur
	}	
}

get-editor "TiApp Editor" | get-section "Apple watchOS App" | get-control -kind "Button" | get-property caption -raw | eq "Create New..." | assert-true -message "Create New button for WatchOS App not found"

get-editor "TiApp Editor" | get-section "Apple watchOS App" | get-button "Create New..." | click

// Wait for window for WatchOS Project name to pop up and then perform action
try -times 15 -delay 6000 -command {
	with [get-window "Progress Information" | get-window Information] {
    	get-editbox -after [get-label "What's the WatchOS project name?"] | set-text $watchOSProjName
    	get-button OK | click
	}
} -catch {
	log -message "Watch OS Project Creation failure. Time Out." -severity error
}

//Wait until window for Watch OS Project creation progress is closed
loop {
	if [get-window | get-property "getShell().getText()" -raw | eq "Progress Information"] {
		wait 1000
		recur
	}	
}

get-editor "TiApp Editor" | get-section "Apple watchOS App" |
    get-editbox -after [get-label "App Location:"] | get-property text -raw
        | eq [concat "extensions/" $watchOSProjName "/" $watchOSProjName ".xcodeproj"] | assert-true -message "Incorrect Watch OS project name created"
    
get-editor "TiApp Editor" | get-section "Apple watchOS App" | 
	get-control -kind "Button" | get-property caption -raw | eq "Configure Provisioning Profiles..." | assert-true -message "Configure Provisioning Profiles button not shown"


get-view "Project Explorer" | get-tree | get-item "TestAlloyApp/extensions" | get-property caption -raw | equals extensions 
    | verify-true

with [get-view "Project Explorer" | get-tree] {
    get-item "TestAlloyApp/extensions" | get-property "getItemCount()" | equals 1 | verify-true
    get-item "TestAlloyApp/extensions" | get-property "getItems().TreeItem[0].getText()" | equals $watchOSProjName 
        | verify-true
    get-item [concat "TestAlloyApp/extensions/" $watchOSProjName] | get-property "getItemCount()" | equals 3 | verify-true
    get-item [concat "TestAlloyApp/extensions/" $watchOSProjName] | get-property "getItems().TreeItem[0].getText()" 
        | equals [concat $watchOSProjName " WatchKit App"] | verify-true
    get-item [concat "TestAlloyApp/extensions/" $watchOSProjName] | get-property "getItems().TreeItem[1].getText()" 
        | equals [concat $watchOSProjName " WatchKit Extension"] | verify-true
    get-item [concat "TestAlloyApp/extensions/" $watchOSProjName] | get-property "getItems().TreeItem[2].getText()" 
        | equals [concat $watchOSProjName ".xcodeproj"] | verify-true    
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
